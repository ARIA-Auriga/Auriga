//==============================================================================
// RagnarokOnline Zonda Script
//==============================================================================

//==========================================
// リヒタルゼン-ジョンダ職員
//------------------------------------------
lighthalzen.gat,94,248,4	script	ジョンダ職員#Zonda	874,{
	cutin "zonda_01",2;
	mes "[ジョンダ職員]";
	mes "いらっしゃいませ。";
	mes "（有）ジョンダイベントは、常に";
	mes "最高のサービスを提供しております。";
	mes "どのサービスをご利用いたしますか？";
	next;
	switch(select("位置セーブサービス","倉庫サービス","カートサービス","終了")) {
	case 1:
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
		if('@map$ == "lighthalzen.gat")
			savepoint "lighthalzen.gat",94,240;
		else if('@map$ == "hugel.gat")
			savepoint "hugel.gat",97,153;
		mes "[ジョンダ職員]";
		mes "セーブいたしました。";
		mes "ご利用頂きまして、";
		mes "誠にありがとうございます。";
		break;
	case 2:
		if(basicskillcheck() && getskilllv(1) < 6) {
			mes "[ジョンダ職員]";
			mes "誠に申し訳ございません、お客様。";
			mes "倉庫は、基本スキルレベル6を";
			mes "習得してから利用可能となります。";
			break;
		}
		if(Zeny < 40) {
			mes "[ジョンダ職員]";
			mes "お客様、倉庫利用料は";
			mes "40 Zenyとなっております。";
			mes "大変申し訳ございませんが、";
			mes "所持金をお確かめの上、";
			mes "再度話し掛けてください。";
			break;
		}
		set Zeny,Zeny-40;
		mes "[ジョンダ職員]";
		mes "ご利用頂きまして、";
		mes "誠にありがとうございます。";
		close2;
		openstorage;
		cutin "zonda_01",255;
		end;
	case 3:
		if(Class != 5 || Class != 10 || Class != 18) {
			mes "[ジョンダ職員]";
			mes "お客様、大変申し訳ございませんが、";
			mes "カートサービスは、商人とその上位職";
			mes "専用のサービスとなっております。";
			break;
		}
		if(checkcart()) {
			mes "[ジョンダ職員]";
			mes "大変申し訳ございませんが、";
			mes "お客様は現在カートを";
			mes "ご利用されております。";
			break;
		}
		mes "[ジョンダ職員]";
		mes "カート利用料金は 800 Zenyです。";
		mes "また、カートを利用するには";
		mes "「プッシュカート」スキルが必要です。";
		mes "そのスキルがないとカートを利用";
		mes "できずにお金だけ消えてしまいます。";
		mes "準備はよろしいですか？";
		next;
		if(select("利用する","取り消し")==2) {
			mes "[ジョンダ職員]";
			mes "ありがとうございました。";
			mes "またのご利用をお待ちしております。";
			break;
		}
		if(Zeny < 800) {
			mes "[ジョンダ職員]";
			mes "お客様、カート利用料金は";
			mes "800 Zenyです。";
			mes "大変申し訳ございませんが、";
			mes "所持金をお確かめの上、";
			mes "再度話し掛けてください。";
			break;
		}
		set Zeny,Zeny-800;
		setcart;
		break;
	case 4:
		mes "[ジョンダ職員]";
		mes "（有）ジョンダイベントは、お客様に";
		mes "常に最高のサービスを";
		mes "提供いたしております。";
		mes "ご意見・ご要望などございましたら、";
		mes "いつでもおっしゃってください。";
		next;
		mes "[ジョンダ職員]";
		mes "ご利用頂きまして、";
		mes "誠にありがとうございました。";
		break;
	}
	close2;
	cutin "zonda_01",255;
	end;
}

//==========================================
// フィゲル-ジョンダ職員
//------------------------------------------
hugel.gat,88,168,4	duplicate(ジョンダ職員#Zonda)	ジョンダ職員	874
