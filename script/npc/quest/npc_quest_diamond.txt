//==============================================================================
// Ragnarok Online Diamond Gambling Script     by Chiffon
//==============================================================================

mjolnir_02.gat,85,362,4	script	ブラックスミス	726,{
	emotion 18;
	mes "[ドウェイン]";
	mes "わ〜い。";
	mes "と〜っても珍しいものを";
	mes "た〜くさん見つけちゃった！";
	mes "きゃはは〜、何だと思う？";
	mes "なんと、ダイヤモンド３カラット!!";
	mes "いいでしょ〜、これで私も大金持ち！";
	next;
	if(select("ダイヤモンドを買いたい！","おめでとう！")==2) {
		mes "[ドウェイン]";
		mes "ありがとう〜！";
		mes "ダイヤモンドが必要になったら";
		mes "いつでもおいで。特別に安い";
		mes "値段で売ってあげるよ。";
		close;
	}
	mes "[ドウェイン]";
	mes "おお!?";
	mes "価値がわかるんだ〜！";
	mes "じゃあ……特別に１個";
	mes "55,000zenyで売ってあげよう！";
	mes "どう？　お買い得でしょ。";
	next;
	mes "[ドウェイン]";
	mes "どれくらい欲しい？";
	mes "あ〜でも500個以下にしてね。";
	mes "多すぎても持てないしさ。";
	mes "それから、要らない時は";
	mes "「0」って言ってね。";
	while(1) {
		next;
		input '@num;
		if('@num <= 0) {
			mes "[ドウェイン]";
			mes "え、いらないの〜？";
			mes "……じゃ、またね！";
			close;
		}
		if('@num > 500) {
			mes "[ドウェイン]";
			mes "むぅ〜。";
			mes "500個以下にしてちょうだい！";
			continue;
		}
		break;
	}
	set '@price,'@num*55000;
	if(Zeny < '@price) {
		mes "[ドウェイン]";
		mes "あっ……えっと、";
		mes "お金が足りないと売れないよ。";
		mes "私も損な商売をするわけには";
		mes "いかないからさ。";
		close;
	}
	if(checkweight(732,'@num)==0) {
		mes "[ドウェイン]";
		mes "けっこう大荷物ね……";
		mes "それじゃあ、ダイヤモンドを";
		mes "持てないでしょ。ちょっと荷物を";
		mes "減らしてから来てちょうだい。";
		close;
	}
	set Zeny,Zeny-'@price;
	getitem 732,'@num;
	mes "[ドウェイン]";
	mes "毎度あり〜!!";
	mes "また必要になったらいつでも来てね！";
	close;
}

comodo.gat,210,154,4	script	怪しい男	118,{
	if(Class == 6 || Class == 12 || Class == 17) {
		mes "[ケイム]";
		mes "ほ〜、同業者か。こんな所で会ったのも";
		mes "何かの縁だ。耳寄りの情報を教えて";
		mes "やろう。本来なら金をとるところだが、";
		mes "そこは同業者のよしみ、特別に";
		mes "無料にしてやろう。ありがたいだろ？";
		mes "耳をかっぽじってよく聞けよ。";
		next;
		mes "[ケイム]";
		callsub L_Routine;
		next;
		mes "[ケイム]";
		mes "その他、もっと珍しいアイテムも";
		mes "持っているらしい。どうだ？";
		mes "俺と一緒にあの嬢ちゃんに";
		mes "探りを入れにいかないか？";
		next;
		if(select("暇だね","よし、一緒に行こう！")==1) {
			mes "[ケイム]";
			mes "チェッ、どうせお前も俺と";
			mes "同じシーフ出身者じゃないか。";
			mes "何を遠慮してるんだ？";
			mes "……そうかよ、俺一人でもその";
			mes "嬢ちゃんから聞き出してやるよ。";
			close;
		}
		mes "[ケイム]";
		mes "ははは。冗談だよ、冗談。";
		mes "そんな、真に受けるなよ。";
		mes "今の言葉は忘れてくれ。";
		next;
		mes "[ケイム]";
		mes "さて、その嬢ちゃんが欲しがっている";
		mes "ダイヤモンド３カラットは";
		mes "どこで手に入るのか、だが……";
		mes "俺の仲間によると、何とかっていう";
		mes "山脈の近くに鉱山があるらしい。";
		mes "そこでなら入手できるかもしれないな。";
		next;
		mes "[ケイム]";
		mes "ダイヤモンドと引き換えに、";
		mes "嬢ちゃんから貴重なアイテムを頂く。";
		mes "な、悪くない話だろ？";
		mes "それじゃ、またな。";
		close;
	}
	else {
		mes "[ケイム]";
		mes "ちょいと耳寄りな情報があるんだが……";
		mes "どうだ？興味ないかい？";
		mes "情報料として500zenyくれれば";
		mes "教えてやるよ。";
		next;
		if(select("わかった、払うよ","いや、やめとく")==2) {
			mes "チェッ、いらないのかよ。";
			mes "今どきいい情報を得るためには";
			mes "金が必要なんだよ、金が。";
			mes "金をけちって、いい情報を";
			mes "逃すなんて馬鹿げてるぞ。";
			close;
		}
		if(Zeny < 500) {	//未調査
			mes "[ケイム]";
			mes "おいおい、500zenyもないのかよ…";
			mes "それじゃあ教えられないぜ。";
			close;
		}
		set Zeny,Zeny-500;
		mes "[ケイム]";
		mes "ありがとよ！";
		mes "んじゃ、教えてやる。";
		callsub L_Routine;
		next;
		mes "[ケイム]";
		mes "その他、もっと珍しいアイテムも";
		mes "持っているらしい。一度あの";
		mes "嬢ちゃんに会ってみるといいよ。";
		next;
		mes "[ケイム]";
		mes "さて、その嬢ちゃんが欲しがっている";
		mes "ダイヤモンド３カラットは";
		mes "どこで手に入るのか、だが……";
		mes "俺の仲間によると、何とかっていう";
		mes "山脈の近くに鉱山があるらしい。";
		mes "そこでなら入手できるかもしれないな。";
		close;
	}
L_Routine:
	mes "あっちにダイヤモンド３カラットを";
	mes "欲しがっている嬢ちゃんがいる。";
	mes "噂によると……彼女はけっこう";
	mes "いい物を持っているらしい。";
	next;
	mes "[ケイム]";
	mes "それが、めったに手に入らない物や";
	mes "高価な物をわんさかとだってさ。前に";
	mes "ちらっと見たところ、山羊の角のような";
	mes "物が生えた兜や王冠を持っていたよ。";
	return;
}

comodo.gat,219,158,4	script	カチュア	91,{
	cutin "katsua01",2;
	mes "[カチュア]";
	mes "以前に見た、あのとても大きな";
	mes "ダイヤモンド。きれいだったなー。";
	mes "あれは３カラットくらいかしら。";
	mes "すごく欲しいな……どうすれば";
	mes "手に入るのかしら？";
	next;
	if(select("あげましょうか？","元気を出してください")==2) {
		mes "[カチュア]";
		mes "慰めてくれてありがとう。";
		mes "ああー、もしそのダイヤモンドが";
		mes "手に入るのなら、私の大事な";
		mes "コレクションすら喜んで";
		mes "差し出すのに……";
		close2;
		cutin "katsua01",255;
		end;
	}
	if(Weight*2 >= MaxWeight) {
		mes "[カチュア]";
		mes "あら、そんなにたくさんの荷物が";
		mes "あると、お礼をお渡しできないわ。";
		mes "少し倉庫に預けてきてくださいね。";
		close2;
		cutin "katsua01",255;
		end;
	}
	if(countitem(732)<1) {
		cutin "katsua03",2;
		mes "[カチュア]";
		mes "ふん！";
		mes "本当はダイヤモンドを持って";
		mes "いないのに、どうして持っている";
		mes "かのような素振りをするの？";
		mes "私を馬鹿にするつもりですか？";
		close2;
		cutin "katsua03",255;
		end;
	}
	cutin "katsua02",2;
	mes "[カチュア]";
	mes "ええっ?!";
	mes "本当にいいんですか？";
	mes "ありがとうございますー。";
	mes "ではお礼に、私のコレクションを";
	mes "一つ差し上げましょう。";
	next;
	mes "[カチュア]";
	mes "どちらをお望みでしょうか？";
	next;
	switch (select("武器","鎧","肩にかける物","兜","靴","盾")) {
		case 1: getitem DIAMOND_WEAPON,1; 	break;
		case 2: getitem DIAMOND_ARMOR,1; 	break;
		case 3: getitem DIAMOND_HOOD,1; 	break;
		case 4: getitem DIAMOND_HELM,1; 	break;
		case 5: getitem DIAMOND_SHOES,1; 	break;
		case 6: getitem DIAMOND_SHIELD,1; 	break;
	}
	delitem 732,1;
	mes "[カチュア]";
	mes "わあー、ダイヤモンドは";
	mes "とてもきれいでいいわよねー";
	mes "また欲しいわ……";
	close2;
	cutin "katsua02",255;
	end;
}

comodo.gat,204,148,0	script	ダビリン	90,{
	mes "[ダビリン]";
	mes "以前、旅行客がカチュア姉さんに";
	mes "とても大きなダイヤモンドを";
	mes "見せたことがあるの。";
	mes "その後、姉さんはずっと";
	mes "あんな風にダイヤモンドだけを";
	mes "追い求めてるんです。";
	next;
	mes "[ダビリン]";
	mes "姉さんはかなり執着心が強いので……";
	mes "ダイヤモンドを手に入れる為なら";
	mes "何でも……そう自分の大切な";
	mes "コレクションさえも差し出す";
	mes "つもりみたい。";
	next;
	mes "[ダビリン]";
	mes "姉さんは昔からコレクターだったの。";
	mes "お金さえあれば、すぐに気に入った物を";
	mes "買っていたわ。収集欲にとらわれて";
	mes "いつかお金を使い果たさないか……";
	mes "とてもそれが心配なの。";
	close;
}
