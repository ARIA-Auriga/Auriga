//= Athena Script ==============================================================
// Ragnarok Online Geffen Town Script	2004-09-10	by Kalen
//==============================================================================

//============================================================
// Shop
//------------------------------------------------------------
geffen_in.gat,77,167,0,	shop	道具商人	68,1750:1,611:40,501:50,502:200,503:550,504:1200,506:40,645:800,656:1500,601:60,602:300,2241:5000
geffen_in.gat,77,173,0,	shop	魔法商人	64,717:600,1601:50,1604:2500,1607:9500,1610:45000,2232:7500,2321:8000,2332:7000
geffen_in.gat,26,178,4,	shop	防具商人	66,2628:400,2101:500,2107:60000,2401:400,2501:1000,2230:50000,2301:10,2303:200,2305:1000,2321:8000,2332:7000
geffen_in.gat,30,178,4,	shop	武器商人	47,1750:1,1751:3,1101:100,1701:1000,1201:50,1204:1250,1207:2400,1210:8500,1213:14000,1216:19500,1601:50,1604:2500,1607:9500,1610:45000
geffen_in.gat,74,144,0,	shop	商人	66,911:160,910:40,912:480
geffen_in.gat,171,123,4	shop	道具商人	64,1092:3,1093:10
geffen.gat,193,152,4,	shop	テイミング商人	124,537:1000,643:3000,10013:1500,10014:2000
geffen_in.gat,22,171,6	shop	武器商人	84,1146:42000,1245:40000

//============================================================
// ゲフェンNPC
//------------------------------------------------------------
geffen.gat,59,143,0	script	女性	91,{
	mes "[女性]";
	mes "草原で生きるハチのホーネットは、";
	mes "一生、女王蜂の命令で蜜を集めるの。";
	mes "これがとっても甘くて栄養もたっぷり";
	mes "なので、けっこう高価なもの";
	mes "なんですって。";
	next;
	mes "[女性]";
	mes "でもね、女王蜂しか食べられない";
	mes "ローヤルゼリーは、ハチ蜜なんかとは";
	mes "比べられないほど良い物だそうよ。";
	close;
}

geffen.gat,156,190,0	script	青年	82,{
	mes "[青年]";
	mes "魔法に関する研究をしていた時に";
	mes "読んだ、ある古文書の伝説によると、";
	mes "この世界の元になったと言われている";
	mes "「イグドラシル」という木の葉と種、";
	mes "そして実は生きている全ての生命に";
	mes "深く関係しているんだそうだ……";
	next;
	mes "[青年]";
	mes "まぁ、俺は見たことも、";
	mes "触ったこともないんだけどね。";
	close;
}

geffen.gat,147,26,0	script	青年	97,{
	mes "[青年]";
	mes "この世界のどこかに";
	mes "使用者の精神力を物理的な力に";
	mes "換えて、高い破壊力を発揮する";
	mes "幻の杖が存在すると聞いたな。";
	next;
	mes "[青年]";
	mes "そんな杖があれば、腕力が弱い";
	mes "人でも、精神力だけで大きい力が";
	mes "出せるのか？不思議なことだ。";
	close;
}

geffen.gat,111,48,0	script	女性	101,{
	mes "[女性]";
	mes "ここゲフェンから南の方へ進んだら、";
	mes "大きい森があります。";
	mes "そこにはオークというモンスターが";
	mes "住んでいるそうです。";
	next;
	mes "[女性]";
	mes "オークは人間のように独特な文化と";
	mes "言葉を持っているそうです。";
	close;
}

//============================================================
// ゲフェン室内NPC
//------------------------------------------------------------
geffen_in.gat,34,170,0	script	青年	47,{
	mes "[青年]";
	mes "魔法を使っている時、長い射程を";
	mes "持った敵が邪魔だろう？";
	mes "だから、敵の武器や鎧を傷つけたり";
	mes "壊したりすることができる武器を";
	mes "考えているんだ。しかし、";
	mes "あまり上手くいかないんだよな……。";
	next;
	mes "[青年]";
	mes "何か他にもっといい方法が";
	mes "あるんじゃないかなぁ……";
	close;
}

geffen_in.gat,27,134,5	script	女性従業員	91,{
	mes "[女性従業員]";
	mes "まったくもう〜お酒飲んだら、";
	mes "あぁなるのは分かっているって";
	mes "いうのに……。";
	mes "おじさん！営業の邪魔！";
	next;
	mes "[女性従業員]";
	mes "本当に、あのおじさんは頭痛の";
	mes "種ですよ〜。姉さんから聞いたんだけど";
	mes "旅館にもあんなのがもう一人";
	mes "いるんですって。";
	next;
	mes "[女性従業員]";
	mes "街にあんなのが二人もいるなんて……";
	mes "開いた口が塞がらないわ。";
	mes "二人で一緒に、どっか遊びにでも";
	mes "行ってくれれば良いのに……。";
	next;
	if(select("飲み物をお願いします","面白い噂はないですか？")==1) {
		mes "[女性従業員]";
		mes "ごめんなさ〜い。あのヨッパライが";
		mes "お酒と言うお酒、";
		mes "全部飲んじゃって……。";
		mes "開店するなり入ってきて、お酒を";
		mes "ぜーんぶ飲んじゃうのよ。";
		next;
		mes "[女性従業員]";
		mes "ごめんね〜また別の機会にでも";
		mes "飲みに来てね。しくしく……。";
		close;
	}
	switch(rand(3)) {
	case 0:
		mes "[女性従業員]";
		mes "最近、この店に珍しく商人が";
		mes "たくさん来るんですよ〜。";
		mes "シュバルツバルドの商人とか……";
		mes "ミッドガッツに商売しに来たって";
		mes "言うんですよね。";
		next;
		mes "[女性従業員]";
		mes "まだ正式貿易許可が下りてないから";
		mes "こっそり品物見せてくれたんですけど。";
		mes "本当にビックリ！";
		mes "初めて見るものばかりだったんです〜。";
		next;
		mes "[女性従業員]";
		mes "近いうちに本格的に商売が";
		mes "始まったら……凄いでしょうね〜。";
		mes "うん……それを見越して、貯金";
		mes "しとこっかな？";
		close;
	case 1:
		mes "[ウィリアム]";
		mes "私の娘に何をしている！";
		next;
		mes "[女性従業員]";
		mes "……………………………………はっ!!";
		mes "ウウ、またお父さんが来ちゃった……。";
		mes "お父さんったら、誤解だってば！";
		next;
		mes "[ウィリアム]";
		mes "そんな事はなぁああああい!!";
		close;
	case 2:
		mes "[女性従業員]";
		mes "う〜ん……この頃、あまり";
		mes "新しい話ってないかな〜。";
		mes "最近、冒険者達の口が重く";
		mes "なったんですよ。うん、";
		mes "そういえばあなたもかなり";
		mes "無口なタイプっぽいかな。";
		next;
		mes "[女性従業員]";
		mes "言えない理由って";
		mes "あるんでしょうけど……";
		mes "店の中で暮らしてるような、";
		mes "私達みたいなのは退屈なんですよ〜。";
		mes "だから、なにか面白い話とか噂を";
		mes "知ってたら、是非教えてくださいね！";
		close;
	}
}

geffen_in.gat,22,125,1	script	ヨッパライおじさん	52,{
	mes "[ヨッパライおじさん]";
	mes "ヨォ、お前も魔法使い卵になりたくて";
	mes "ここに来たか〜。ヒック";
	close;
}

geffen_in.gat,70,67,3	script	女性従業員	90,{
	mes "[女性従業員]";
	mes "あぁ〜、もう！";
	mes "どうして居酒屋じゃなくて";
	mes "こっちでお酒を飲むのかしら！";
	mes "ここが旅館か居酒屋かくらい、";
	mes "わかってるはずなのに。";
	next;
	mes "[女性従業員]";
	mes "あのおじさんね、お酒を飲んでは、";
	mes "あのように愚痴ってばかり！";
	mes "おじさん！営業の邪魔！";
	next;
	mes "[女性従業員]";
	mes "本当に、あのおじさんは頭痛の";
	mes "種ですよ〜。居酒屋で働いてる妹に";
	mes "聞いたんですけど、あっちにも";
	mes "同じようなのがいるんですって。";
	next;
	mes "[女性従業員]";
	mes "街にあんなのが二人もいるなんて……";
	mes "なにか間違ってるわ！";
	mes "二人で一緒に、どっか遊びにでも";
	mes "行ってくれれば良いのに……。";
	next;
	if(select("飲み物をお願いします","面白い噂はないですか？")==1) {
		mes "[女性従業員]";
		mes "ごめんなさ〜い。あのヨッパライが";
		mes "お酒と言うお酒、全部飲んじゃって……";
		mes "お酒をがあるって一言だけで飛んできて、";
		mes "お酒をぜーんぶ飲んじゃうのよ。";
		next;
		mes "[女性従業員]";
		mes "ごめんね〜また別の機会にでも";
		mes "飲みに来てね。しくしく……。";
		close;
	}
	switch(rand(3)) {
	case 0:
		mes "[女性従業員]";
		mes "そうですね〜。";
		mes "最近、商人さんの宿泊客が";
		mes "目立ちますね〜。";
		mes "パッと見るからに、この国の人じゃ";
		mes "ないって分かるんですよ。";
		next;
		mes "[女性従業員]";
		mes "妹に話したら、シュバルツバルドの";
		mes "人達だって言ってました〜。";
		mes "お客さんが増えるなら、私達は";
		mes "シュバルツバルドの人だろうが";
		mes "ミッドガッツの人だろうが、";
		mes "構いませんけどね。";
		next;
		mes "[女性従業員]";
		mes "早く本格的な貿易が";
		mes "始まれば良いのに〜。";
		close;
	case 1:
		mes "[ウィリアム]";
		mes "今度は、こっちの娘にまで";
		mes "手を出す気か〜っ!!!!";
		next;
		mes "[女性従業員]";
		mes "えっ………………………………………";
		mes "キャアァアァアァッ!!";
		mes "い、いったい何の話?!";
		mes "あなた！あなた何かやったのね！";
		mes "気持ち悪い！今すぐ出てって!!";
		close;
	case 2:
		mes "[女性従業員]";
		mes "う〜ん……この頃、あまり";
		mes "新しい話ってないかな〜。";
		mes "最近、冒険者達の口が重く";
		mes "なったんですよ。うん、";
		mes "そういえばあなたもかなり";
		mes "無口なタイプっぽいかな。";
		next;
		mes "[女性従業員]";
		mes "言えない理由って";
		mes "あるんでしょうけど……";
		mes "店の中で暮らしてるような、";
		mes "私達みたいなのは退屈なんですよ〜。";
		mes "だから、なにか面白い話とか噂を";
		mes "知ってたら、是非教えてくださいね！";
		close;
	}
}

geffen_in.gat,59,61,1	script	ヨッパライおじさん	61,{
	mes "[ヨッパライおじさん]";
	mes "うん……お前もゼンハイザの友達か？";
	next;
	switch(select("いいえ","それ、誰ですか？","私、友達いません")) {
	case 1:
		mes "[ヨッパライおじさん]";
		mes "うん……そういえばアイツに";
		mes "友達がいるはずないか。";
		mes "気にせずに楽しんでいきなさい。";
		mes "ヒック";
		close;
	case 2:
		mes "[ヨッパライおじさん]";
		mes "うん、そいつはこのホールの";
		mes "左の部屋へ行けば会える陰気な奴だ。";
		mes "一日中暗い部屋で変な設計図と";
		mes "睨めっこしてるんだよ。";
		next;
		mes "[ヨッパライおじさん]";
		mes "耳にかぶせるとか、作るとか";
		mes "作らないとか、訳のわからない話を";
		mes "しているんだ〜。";
		next;
		mes "[ヨッパライおじさん]";
		mes "興味があれば、一度見物して";
		mes "みるといい。フフ……。";
		mes "彼が作るあの二つの物〜、";
		mes "良い音がするんだ……フフフ〜。";
		close;
	case 3:
		mes "[ヨッパライおじさん]";
		mes "……あ、ああそうなのか！";
		mes "スマンスマン、痛い所を";
		mes "突いてしまったね……！";
		next;
		mes "[ヨッパライおじさん]";
		mes "あぁ……友達までいないなんて。";
		mes "君は本当に若い時の自分にそっくりだ！";
		mes "君は将来、私のようになるのか……";
		mes "しくしく……ウフ、ヒックヒック";
		next;
		mes "[ヨッパライおじさん]";
		mes "ウアアアアアアアァアアアアアアン!!";
		next;
		mes "[女性従業員]";
		mes "ああ！うるさいです！";
		close;
	}
}

geffen_in.gat,79,76,2	script	商人デイブン	120,{
	mes "[商人デイブン]";
	mes "私が初めてゲフェンに来た時";
	mes "ここはただ退屈な街だったのになー。";
	next;
	mes "[商人デイブン]";
	mes "ところが今じゃ気高そうな魔法使い達と";
	mes "気難しそうな鍛冶職人達が集まって";
	mes "カチカチコチコチ退屈な都市に";
	mes "なってしまったなー。";
	mes "ハハッ！本当に退屈だなー！";
	next;
	switch(select("魔法使い？","鍛冶職人？","おじさんはどなたですか？")) {
	case 1:
		mes "[商人デイブン]";
		mes "魔法使い達はいつも本を片手に暮らす";
		mes "狭量で陳腐な奴等さ。";
		mes "大きく、マジシャンとウィザードに";
		mes "分けられるんだって。";
		next;
		if(select("マジシャン？","ウィザード？")==1) {
			mes "[商人デイブン]";
			mes "マジシャン達はこの都市北西の方の";
			mes "魔法学校に集まっている。";
			mes "基礎的な勉強をして、資格を取って";
			mes "魔法を使うことが出来るってさ。";
		}
		else {
			mes "[商人デイブン]";
			mes "ウィザード達は魔法学校で学んだ";
			mes "知識以上の経験と知識を持った";
			mes "マジシャンがなる職業さ。";
			mes "マジシャンより強くて偉い魔法を";
			mes "使うことが出来る。";
			next;
			mes "[商人デイブン]";
			mes "ウィザード達はゲフェンタワーの";
			mes "最上層に集まっているよ。";
		}
		close;
	case 2:
		mes "[商人デイブン]";
		mes "鍛冶ねぇ……本当にブラックスミス達は";
		mes "気難しい奴等だ。奴等は人と会う時も、";
		mes "いつも顔に炭をつけてて汚いし。";
		mes "あの変なにおいもするし。";
		next;
		mes "[商人デイブン]";
		mes "しかし、それも仕方ない事";
		mes "なんだけどね。";
		mes "いつもいつも、鉱石とか鍛冶工具に";
		mes "触ってるし。奴等がそこまで努力する";
		mes "には、ちゃ〜んと理由があるのさ。";
		next;
		mes "[商人デイブン]";
		mes "「鉱石を研いで、自分だけの銘柄の";
		mes "特殊な武器を作り上げたり！」";
		next;
		mes "[商人デイブン]";
		mes "ブラックスミスの名前が付いた武器は";
		mes "普通の商店で売っている武器と水準が";
		mes "違う。その武器は、まったく違った";
		mes "威力と意味を持っている。";
		mes "だから人々は、どれだけ高くても、";
		mes "ブラックスミスの武器を買うのさ！";
		close;
	case 3:
		mes "[商人デイブン]";
		mes "私がこの世で一番、キレイな露天商〜。";
		mes "エヘ〜。";
		next;
		menu "……マグナムブレイク！",-;
		mes "[商人デイブン]";
		mes "ば、ば、馬鹿〜！";
		close;
	}
}

geffen_in.gat,114,73,1	script	シュバルツバルド商人	709,{
	mes "[ハンス=ハデンハイム]";
	mes "お元気ですか、ルーン=ミッドガッツの";
	mes "若者さん。";
	mes "私はシュバルツバルドから来た商人、";
	mes "ハンス=ハデンハイムと申します。";
	next;
	mes "[ハンス=ハデンハイム]";
	mes "私達は、ここゲフェンと故郷を行き来";
	mes "しながら商売をするつもりなのです。";
	next;
	mes "[ハンス=ハデンハイム]";
	mes "ミッドガッツには見たこともない";
	mes "不思議な品物が多いのですよ。";
	mes "逆に言えば……私達が普通に故郷で";
	mes "手に入れることができる品が、";
	mes "こちらでは珍しい宝物になっていたり";
	mes "します。";
	next;
	mes "[ハンス=ハデンハイム]";
	mes "こちらにはなくて、あちらにはあって";
	mes "そのような事が品物を扱う楽しみだと";
	mes "言えます。";
	mes "そして、そんな品物達を行き来させて";
	mes "世の中を豊かにすること！それが私たち";
	mes "貿易商の義務だと自負しています。";
	next;
	mes "[ハンス=ハデンハイム]";
	mes "もし、ミッドガッツの品で満足出来ない";
	mes "ことがあれば、私達がこちらへ";
	mes "進出する日をお待ちください。";
	mes "良い品々を、たくさん仕入れて";
	mes "参りましょう。";
	close;
}

//============================================================
// ブラックスミスギルドNPC
//------------------------------------------------------------
geffen_in.gat,110,172,0	script	クリストファー	63,{
	mes "[クリストファー ギレンロウ]";
	mes "クリストファーの鍛冶屋にようこそ。";
	mes "ここは精錬に関するすべての";
	mes "品物を取り扱っている。";
	mes "さあ……何しに来たんだい？";
	next;
	switch(select("金敷の購入","精錬品の購入","金属の購入","原石の精製","やめる")) {
	case 1:
		mes "[クリストファー ギレンロウ]";
		mes "武器製作に必要な金敷は";
		mes "硬ければ硬いほどいいものだ。";
		mes "だが、硬いものほど高い。";
		mes "無理せずに必要な物を選ぶんだ。";
		next;
		switch(select("金敷 - 30000 Zeny","オリデオコンの金敷 - 120000 Zeny",
					"黄金の金敷 - 300000 Zeny","もっと硬い金敷","取引をやめる")) {
		case 1:
			set '@itemid,986;
			set '@price,30000;
			break;
		case 2:
			set '@itemid,987;
			set '@price,120000;
			break;
		case 3:
			set '@itemid,988;
			set '@price,300000;
			break;
		case 4:
			mes "[クリストファー ギレンロウ]";
			mes "俺の店に黄金の金敷より";
			mes "硬いやつは売ってない。";
			next;
			mes "[クリストファー ギレンロウ]";
			mes "伝説の金敷製作者「リンゲル」が";
			mes "作った物があるとすれば話が";
			mes "違うけど……これ以上硬い金属で";
			mes "作られたものは俺の店だけではなく、";
			mes "どこを探しても無いはずだ。";
			close;
		case 5:
			mes "[クリストファー ギレンロウ]";
			mes "何か必要なものがあったら";
			mes "話し掛けてな。それじゃ。";
			close;
		}
		if(Zeny < '@price) {
			mes "[クリストファー ギレンロウ]";
			mes "君が持っているお金では……";
			mes "無理だな。損する気はないんだ。";
			close;
		}
		if(checkweight('@itemid,1)==0) {
			mes "[クリストファー ギレンロウ]";
			mes "君……顔色悪いな……。";
			mes "品物を受け取る前に、";
			mes "荷物を整理した方がいい。";
			close;
		}
		set Zeny,Zeny-'@price;
		getitem '@itemid,1;
		switch(@menu) {
		case 1:
			mes "[クリストファー ギレンロウ]";
			mes "値段も安いし、なかなか手頃だな。";
			break;
		case 2:
			mes "[クリストファー ギレンロウ]";
			mes "やっぱり、目が高いな。";
			mes "鍛冶屋ならこの位の金敷は";
			mes "必需品だな。";
			break;
		case 3:
			mes "[クリストファー ギレンロウ]";
			mes "最高に硬く、美しい金属で作られた";
			mes "この金敷を選ぶとは!（感激）";
			mes "これさえあれば、君は立派な";
			mes "鍛冶屋になれる!";
			break;
		}
		break;
	case 2:
		mes "[クリストファー ギレンロウ]";
		mes "立派な鍛冶屋には立派な道具！";
		mes "このクリストファーの鍛冶屋で";
		mes "道具を買うなんて、君には本当に";
		mes "良い鍛冶屋になる将来性がある。";
		mes "好きなやつを選んでくれ。";
		next;
		switch(select("携帯用溶鉱炉 - 150 Zeny","鋼鉄の金槌 - 1000 Zeny",
					"黄金の金槌 - 3000 Zeny","オリデオコンの金槌 - 5000 Zeny","取引をやめる")) {
		case 1:
			mes "[クリストファー ギレンロウ]";
			mes "金属精製にはこいつだ！";
			next;
			set '@itemid,612;
			set '@price,150;
			break;
		case 2:
			set '@itemid,613;
			set '@price,1000;
			break;
		case 3:
			set '@itemid,614;
			set '@price,3000;
			break;
		case 4:
			set '@itemid,615;
			set '@price,5000;
			break;
		case 5:
			mes "[クリストファー ギレンロウ]";
			mes "何か必要なものがあったら";
			mes "話し掛けてな。それじゃ！";
			close;
		}
		mes "[クリストファー ギレンロウ]";
		mes "さあ、どの位買う？";
		mes "やめるんだったら<0>って";
		mes "書いてくれ。";
		while(1) {
			next;
			input '@num;
			if('@num < 0 || '@num > 500) {
				mes "[クリストファー ギレンロウ]";
				mes "500個以下にしてくれ。";
				continue;
			}
			break;
		}
		if('@num == 0) {
			mes "[クリストファー ギレンロウ]";
			mes "やめるのか。やむを得んな……。";
			close;
		}
		if(Zeny < '@price*'@num) {
			mes "[クリストファー ギレンロウ]";
			mes "君、お金が足りないな……。";
			mes "俺も損する訳にはいかないからな……。";
			mes "女房がうるさいんだ。";
			close;
		}
		if(checkweight('@itemid,'@num)==0) {
			mes "[クリストファー ギレンロウ]";
			mes "君……顔色悪いな……。";
			mes "品物を受け取る前に、";
			mes "荷物を整理した方がいい。";
			close;
		}
		set Zeny,Zeny-'@price*'@num;
		getitem '@itemid,'@num;
		mes "[クリストファー ギレンロウ]";
		break;
	case 3:
		mes "[クリストファー ギレンロウ]";
		mes "鉄や鋼鉄の武器を製錬する時に";
		mes "使える金属を売っている。";
		mes "レベル1の武器製錬に使える";
		mes "^007777プラコン^000000 と";
		mes "レベル2の武器製錬に使える";
		mes "^007777エンベルタコン^000000 があるぞ。";
		next;
		if(checkweight(1010,10)==0 || checkweight(1011,10)==0) {
			mes "[クリストファー ギレンロウ]";
			mes "荷物が多いな。";
			mes "持ち物を整理してから";
			mes "またおいで。";
			close;
		}
		switch(select("プラコン購入","エンベルタコン購入","他の金属はないか聞いてみる")) {
		case 1:
			mes "[クリストファー ギレンロウ]";
			mes "プラコンは1個200Zenyだ。";
			set '@itemid,1010;
			set '@price,200;
			break;
		case 2:
			mes "[クリストファー ギレンロウ]";
			mes "エンベルタコンは";
			mes "1個1000Zenyだ。";
			set '@itemid,1011;
			set '@price,1000;
			break;
		case 3:
			mes "[クリストファー ギレンロウ]";
			mes "レベル1とレベル2の武器より";
			mes "レベルが高い武器を精錬するための";
			mes "金属だな。";
			mes "もう知っているかもしれんが、 ";
			mes "オリデオコンとエルニウムは";
			mes "手に入れるのがすごく難しくてね……。";
			close;
		}
		next;
		switch(select("1 個購入","2 個購入","5 個購入","10 個セット購入！","取り消す")) {
		case 1: set '@num,1; break;
		case 2: set '@num,2; break;
		case 3: set '@num,5; break;
		case 4: set '@num,10; break;
		case 5:
			mes "[クリストファー ギレンロウ]";
			mes "おう、やめるか。";
			close;
		}
		if(Zeny < '@price*'@num) {
			mes "[クリストファー ギレンロウ]";
			mes "君、お金が足りないな……。";
			mes "俺も損する訳にはいかないからな……。";
			mes "女房がうるさいんだ。";
			close;
		}
		set Zeny,Zeny-'@price*'@num;
		getitem '@itemid,'@num;
		mes "[クリストファー ギレンロウ]";
		break;
	case 4:
		mes "[クリストファー ギレンロウ]";
		mes "精製できる金属は";
		mes "オリデオコンとエルニウムだな。";
		mes "各5つの原石を精製して、";
		mes "純度の高い金属にするんだ。";
		mes "さあ……どの金属にするつもりだい？";
		next;
		switch(select("オリデオコンを作る","エルニウムを作る","やめる")) {
		case 1:
			set '@itemid,756;
			set '@gain,984;
			break;
		case 2:
			set '@itemid,757;
			set '@gain,985;
			break;
		case 3:
			mes "[クリストファー ギレンロウ]";
			mes "何か必要な物があれば、";
			mes "話かけてな。それじゃ、また。";
			close;
		}
		if(countitem('@itemid)<5) {
			mes "[クリストファー ギレンロウ]";
			mes getitemname('@itemid)+ "が5つなければ、";
			mes getitemname('@gain)+ "は作れない。";
			close;
		}
		delitem '@itemid,5;
		getitem '@gain,1;
		mes "[クリストファー ギレンロウ]";
		mes "さあ、" +getitemname('@gain)+ "だ。";
		mes "また必要ならいつでもどうぞ。";
		close;
	case 5:
		mes "[クリストファー ギレンロウ]";
		mes "何か必要な物があれば、";
		mes "話かけてな。それじゃ、また。";
		close;
	}
	mes "とにかく、ありがとう。";
	mes "また必要なものがあったら、";
	mes "話し掛けてくれ。";
	close;
}
