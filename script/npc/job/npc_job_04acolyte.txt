//====================================================================
//Ragnarok Online Acolyte jobchange script
//
//　■ CHANGE_AL -> 0〜6
//     CHANGE_PR -> プリースト転職用
//     OLD_CLASS -> 転生前の職業
//====================================================================

//==========================================
// 試験申請および転職
//------------------------------------------

prt_church.gat,184,41,4	script	聖堂神父	60,{
	if(Upper == 1 && Class == Job_Novice) {
		if(OLD_CLASS != Job_Priest && OLD_CLASS != Job_Monk) {
			//転生前が聖職者系でないとき？未調査？
			close;
		}
		mes "[マルシス神父]";
		mes "多くの試練を乗り越えてきましたね。";
		mes "貴方の活躍については";
		mes "私の耳にも入っております。";
		next;
		if(getskilllv(1) < 9 || SkillPoint) {
			mes "[マルシス神父]";
			mes "さて、今すぐアコライトに";
			mes "転職させてあげたいところですが、";
			mes "基本的なことをもう一度復習してから";
			mes "またいらしてください。";
			next;
			mes "[マルシス神父]";
			mes "それでは、またお会いできるのを";
			mes "楽しみにお待ちしております。";
			close;
		}
		mes "[マルシス神父]";
		mes "聖職者の道を究めんとする";
		mes "貴方を、心から歓迎いたします。";
		next;
		unequip;
		jobchange 4,1;
		skill 156,1,0;
	}
	if(Upper == 1 && Class == Job_Acolyte) {
		mes "[マルシス神父]";
		mes "さあ、これからも神の使いとして";
		mes "いっそう励んでくださいね。";
		mes "貴方の未来に幸多からんことを！";
		close;
	}
	mes "[マルシス神父]";
	mes "どのようなご用件で";
	mes "いらっしゃいましたか？";
	next;
	switch (select("アコライトに転職","アコライト転職条件を聞く","やめる")) {
	case 1:
		if(Class == Job_Acolyte) {
			mes "[マルシス神父]";
			mes "貴方はもしやどこか体の具合が";
			mes "悪いのでは…？その格好は…";
			mes "すでにアコライトではありませんか？";
			mes "まさか今のは冗談でしたと？";
			close;
		}
		if(Class != Job_Novice) {
			mes "[マルシス神父]";
			mes "貴方はもう他の職業を";
			mes "お持ちでは？";
			close;
		}
		break;
	case 2:
		mes "[マルシス神父]";
		mes "貴方はアコライトになりたいのですか？";
		mes "では、私がこれから言う2つの条件を、";
		mes "神様からの試練だと思って克服する";
		mes "のです。";
		next;
		mes "[マルシス神父]";
		mes "まず、初心者スキルを９までに";
		mes "しなければなりません。";
		mes "この世界で生きて行く為の基本的な";
		mes "修行で、自分のためでもあります。";
		next;
		mes "[マルシス神父]";
		mes "次、修行者として試練を";
		mes "克服してください。";
		mes "修行地は、アコライトへの転職を";
		mes "申し込んだら言い渡されます。";
		next;
		mes "[マルシス神父]";
		mes "この３つを完全に遂行しましたら、";
		mes "ここに戻ってください。";
		mes "その時、神の御使いたる聖なる";
		mes "職業に就くことができるでしょう。";
		close;
	case 3:
		close;
	}
	//修練案内ここから
	switch(CHANGE_AL) {
	case 0:
		mes "[マルシス神父]";
		mes "貴方は心から神様のお使いに";
		mes "なるおつもりですか？";
		next;
		if(select("はい。","やっぱり、やめます。")==2) {
			mes "[マルシス神父]";
			mes "それでは。";
			close;
		}
		mes "[マルシス神父]";
		mes "いいでしょう。" +strcharinfo(0)+ " …";
		mes "貴方のアコライト転職申請書を";
		mes "受諾します。";
		mes "貴方は神様のしもべとなるために、";
		mes "ある修練をしなければなりません。";
		next;
		mes "[マルシス神父]";
		mes "貴方は……";
		set CHANGE_AL,rand(1,3);
		break;
	case 1:
	case 2:
	case 3:
		mes "[マルシス神父]";
		mes "おお、また来てくださいましたか。";
		mes "神様の志を遂行できるだけの準備は";
		mes "できていますか…？";
		next;
		if(getskilllv(1) < 9 || SkillPoint) {
			mes "[マルシス神父]";
			mes "なに？！まだ基本的な修練も";
			mes "できていないではありませんか！";
			mes "まだまだです。もっと高い職業レベルが必要です！";
			close;
		}
		mes "[マルシス神父]";
		mes "修行完了者名簿にはまだ貴方の";
		mes "名前が載っておりませんが…。";
		break;
	case 4:
	case 5:
	case 6:
		mes "[マルシス神父]";
		mes "うむ。職業レベルも十分ですし、";
		mes "修行完了者名簿にも名前が載って";
		mes "います。いいでしょう。";
		mes "貴方の意志を認め、アコライトたる";
		mes "資格を与えましょう。";
		unequip;
		jobchange 4;
		set CHANGE_AL,0;
		next;
		mes "[マルシス神父]";
		mes "いつも我々を見守ってくださる神様に";
		mes "感謝して、これからどのような苦難と";
		mes "試練が迫って来ても一層信仰を守って";
		mes "神様に身をささげるのです。";
		next;
		mes "[マルシス神父]";
		mes "あと、貴方が最後まであきらめないで";
		mes "試練を乗り越え、修行を終わらせ、";
		mes "アコライトになった事を心から";
		mes "お祝いします。";
		close;
	}
	//0〜3のとき続き
	switch(CHANGE_AL) {
		case 1:
			mes "^000077プロンテラ北東の聖カピトーリナ修道院前^000000で";
			mes "修行中の ^000077ルバルカバラ^000000 神父に";
			break;
		case 2:
			mes "^000077プロンテラ南西のモロクの辺り^000000で";
			mes "修行中の ^000077マチルダ^000000 シスターに";
			break;
		case 3:
			mes "^000077プロンテラ北西のどこかの橋^000000で";
			mes "修行中の ^000077洋介^000000 神父に";
			break;
	}
	mes "会ってきてください。";
	next;
	mes "[マルシス神父]";
	mes "あの人は、アコライトになるために修行";
	mes "している者を待っているのです。";
	mes "さあ、早く貴方の意志を伝えに行って";
	mes "ください。";
	close;
}


//==========================================
// 聖地巡礼（プリーストと共用）
//------------------------------------------

prt_fild03.gat,365,255,1	script	修行中の神父	89,{
	//アコライト転職
	if(Class == Job_Novice) {
		switch(CHANGE_AL) {
		case 0:
			mes "[ルバルカバラ神父]";
			mes "うん？どうしてこのような場所まで";
			mes "来たのか？アコライト修行者や旅人なら";
			mes "ともかく、初心者には危険な所だから";
			mes "早く帰ったほうがいい。";
			close;
		case 1:
			mes "[ルバルカバラ神父]";
			mes "あ、もし、";
			mes "貴方がアコライトになりたいという";
			mes "若者か？";
			mes "連絡はもう聖堂側から頂いている。";
			mes "貴方の名前は… " +strcharinfo(0)+ "…";
			mes "あった、あった！よくきてくれた。";
			next;
			mes "[ルバルカバラ神父]";
			mes "もうマルシス神父からアコライトについて";
			mes "いろいろ聞いただろうからこれ以上";
			mes "私からは言う必要はないだろう。";
			mes "最近の若者は小言をきくのが嫌だろう？";
			mes "ははは…";
			next;
			mes "[ルバルカバラ神父]";
			mes "しかし、神様のお使いになろうと思うなら";
			mes "そのくらいは我慢した方がいい。";
			mes "なぜなら神様は、あまり寡黙なタイプ";
			mes "ではないんだ。はははは…";
			next;
			mes "[ルバルカバラ神父]";
			mes "つまらない話しはやめて、貴方がここに";
			mes "来たという証明書を作って聖堂に送って";
			mes "おくからもう帰って良いぞ。";
			next;
			mes "[ルバルカバラ神父]";
			mes "くれぐれも気をつけて。";
			set CHANGE_AL,4;
			savepoint "prt_fild03.gat",360,256;
			close;
		case 4:
			mes "[ルバルカバラ神父]";
			mes "貴方が聖堂に着く頃には";
			mes "もう連絡がついているだろう。";
			mes "気をつけて帰って。";
			next;
			mes "[ルバルカバラ神父]";
			mes "鳩に手紙を任したが、大丈夫だろう…";
			close;
		default:
			mes "[ルバルカバラ神父]";
			mes "あ、もし、";
			mes "貴方がアコライトになりたいという";
			mes "若者か？";
			mes "連絡はもう聖堂側から頂いている。";
			mes "貴方の名前は… " +strcharinfo(0)+ "…";
			mes "あれ…ないぞ…";
			next;
			mes "[ルバルカバラ神父]";
			mes "……聖堂に戻って";
			mes "もう一度確認したらどうかな…？";
			close;
		}
	}
	//プリースト転職
	if(Class == Job_Acolyte) {
		switch(CHANGE_PR) {	//npc_job_08priest.txtにて発行
		case 0:
			mes "[ルバルカバラ神父]";
			mes "ごめんなさい。私は今修行中の身。";
			mes "人とお話できません。";
			close;
		case 1:
			mes "[ルバルカバラ神父]";
			mes "プリースト志望のアコライトだな…";
			mes "トーマス司教様から連絡はいただいて";
			mes "いる。巡礼は始まったばかりだが、";
			mes "最期まで諦めずに頑張ってほしい。";
			next;
			mes "[ルバルカバラ神父]";
			mes "もし機会があれば、またいつでも";
			mes "来るといい…。";
			mes "ここは私達にとって聖地だからね。";
			next;
			mes "[ルバルカバラ神父]";
			mes "それでは、次の巡礼地へ向かいなさい。";
			mes "道中くれぐれも気をつけて。";
			set CHANGE_PR,2;
			savepoint "prt_fild03.gat",360,255;
			close;
		case 2:
			mes "[ルバルカバラ神父]";
			mes "はやく次の巡礼地へ向かいなさい。";
			mes "道中くれぐれも気をつけて。";
			close;
		default:
			mes "[ルバルカバラ神父]";
			mes "なぜここへ？";
			mes "貴方には貴方の修行があるはず。";
			close;
		}
	}
	mes "[ルバルカバラ神父]";
	mes "おお？こんな奥地にまで修行に";
	mes "来たのか？それとも旅人か？";
	mes "ここのモンスターは見かけと違い";
	mes "結構強いから気をつけることだ。";
	close;
}

//==============================================================
moc_fild07.gat,41,355,4	script	修行中のシスター	95,{
	//アコライト転職
	if(Class == Job_Novice) {
		switch(CHANGE_AL) {
		case 0:
			mes "[マチルダ]";
			mes "……";
			close;
		case 2:
			mes "[マチルダ]";
			mes "あ、アコライト修行者ですね。";
			mes "ここまでお疲れ様でした。";
			next;
			set CHANGE_AL,5;
			savepoint "moc_fild07.gat",36,355;
			mes "[マチルダ]";
			mes "お名前は何ですか？えっと…";
			mes "あ、ここにありますね！";
			mes strcharinfo(0)+ "さんがここに";
			mes "来たことを証明する証明書を送ります。";
			mes "聖堂に戻ってマルシス神父に";
			mes "確認を取ってください。";
			close;
		case 5:
			mes "[マチルダ]";
			mes "鳩を使って聖堂に証明書を送りました。";
			mes "貴方が聖堂に着く頃にはもう確認が";
			mes "取れているでしょう。";
			mes "貴方が無事、望んだアコライトへ";
			mes "なれるよう心からお祈りいたします。";
			close;
		default:
			mes "[マチルダ]";
			mes "あ、アコライト修行者ですね。";
			mes "ここまでお疲れ様でした。";
			mes "お名前は何ですか？えっと…";
			mes "あれ？見つからない…";
			mes "あの…ここには貴方のお名前が";
			mes "載っていません…";
			next;
			mes "[マチルダ]";
			mes "聖堂に戻ってもう一度確認してきて";
			mes "いただけますか？";
			close;
		}
	}
	//プリースト転職
	if(Class == Job_Acolyte) {
		switch(CHANGE_PR) {	//npc_job_08priest.txtにて発行
		case 0:
			mes "[マチルダ]";
			mes "貴方に神のご加護がありますように…";
			close;
		case 1:
			mes "[マチルダ]";
			mes "巡礼者ですね？";
			mes "しかし私を訪ねる前に、他の方を";
			mes "訪ねなければなりません。";
			next;
			mes "[マチルダ]";
			mes "トーマス司教様やセシル様に伺って";
			mes "みてはいかがでしょう。";
			mes "貴方に神のご加護がありますように…";
			close;
		case 2:
			mes "[マチルダ]";
			mes "あ、プリースト修行者ですね？";
			mes "遠いところまでお疲れ様〜";
			mes "私たちプリーストたちは地上の全てに";
			mes "信義を説かなければなりません。";
			next;
			mes "[マチルダ]";
			mes "プリーストになられたら、是非モロクの";
			mes "ピラミッドにいらっしゃってください。";
			mes "あそこにはたくさんのアンデッドが";
			mes "居ます。";
			mes "どうか彼らを解放してあげてください…";
			next;
			mes "[マチルダ]";
			mes "それではお気をつけて…";
			mes "貴方に神のご加護がありますように…";
			set CHANGE_PR,3;
			savepoint "moc_fild07.gat",36,355;
			close;
		case 3:
			mes "[マチルダ]";
			mes "さ、こちらに留まらず";
			mes "次の巡礼地へ…";
			close;
		default:
			mes "[マチルダ]";
			mes "こちらへはどのようなご用で？";
			mes "修行を続けてください。";
			close;
		}
	}
	mes "[マチルダ]";
	mes "……";
	close;
}

//==============================================================
prt_fild00.gat,208,218,4	script	修行中の神父	98,{
	//アコライト転職
	if(Class == Job_Novice) {
		switch(CHANGE_AL) {
		case 0:
			mes "[洋介神父]";
			mes "そこの初心者、私に言いたい事でも";
			mes "あるのか？";
			close;
		default:
			mes "[洋介神父]";
			mes "そこの君、アコライト修行者か？";
			next;
			mes "[洋介神父]";
			mes "弱いくせによく俺の所にきてくれたな。";
			mes "名前は？ " +strcharinfo(0)+ " か。";
			next;
			if(CHANGE_AL != 3) {
				mes "[洋介神父]";
				mes "人違いみたいだな。";
				mes "…どうせ君の間違いだから、戻って";
				mes "マルシス神父にもう一度聞いてくれ。";
				close;
			}
			set CHANGE_AL,6;
			savepoint "prt_fild00.gat",206,231;
			mes "[洋介神父]";
			mes "いいだろう。 " +strcharinfo(0)+ "。";
			mes "君がここに来たという証明書を送る。";
			mes "聖堂に戻ってマルシス神父に";
			mes "確認したらいい。";
			close;
		case 6:
			mes "[洋介神父]";
			mes "なんだ？私にはもう用はないだろう？";
			mes "用がなかったらさっさと聖堂に戻れ！";
			close;
		}
	}
	//プリースト転職
	if(Class == Job_Acolyte) {
		switch(CHANGE_PR) {	//npc_job_08priest.txtにて発行
		case 0:
			mes "[洋介神父]";
			mes "アコライトか。久しぶりだなー";
			close;
		case 1:
		case 2:
			mes "[洋介神父]";
			mes "うん、プリースト修行者だな。";
			mes "ここまでよく訪ねてきた。";
			next;
			mes "[洋介神父]";
			mes "しかし私を尋ねて来る前に";
			mes "他に会って来るように。";
			mes "早い話が、間違えたってことだ。";
			next;
			mes "[洋介神父]";
			mes "聖堂に帰って確認してみてくれ。";
			close;
		case 3:
			mes "[洋介神父]";
			mes "うん、プリースト修行者だな。";
			mes "ここまでよく訪ねてきた。";
			next;
			mes "[洋介神父]";
			mes "よし、プリーストの名に恥じないよう";
			mes "正しく生きていきなさい。";
			next;
			mes "[洋介神父]";
			mes "聖堂に帰ってよし。";
			mes "君の巡礼はここで最後だ。";
			mes "よくここまで旅を続けたな。";
			mes "帰りもくれぐれも気をつけるように。";
			set CHANGE_PR,4;
			savepoint "prt_fild00.gat",205,231;
			close;
		case 4:
			mes "[洋介神父]";
			mes "ん？聖堂に帰らないのか？";
			mes "それともここで一生過ごしたいのか？";
			close;
		default:
			mes "[洋介神父]";
			mes "こら、こんなところで遊んで";
			mes "いないで早くプリーストになりなさい。";
			close;
		}
	}
	mes "[洋介神父]";
	mes "……";
	close;
}
