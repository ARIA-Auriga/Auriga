//====================================================================
//Ragnarok Online Archer jobchange script
//
//　■ CHANGE_AC -> 0〜1
//     OLD_CLASS -> 転生前の職業
//====================================================================

//==========================================
// 試験申請および転職
//------------------------------------------

payon_in02.gat,64,71,4	script	ギルド関係者	85,{
	mes "[アーチャーギルド関係者]";
	mes "こんにちは、";
	mes "何をお手伝いしましょうか？";
	next;
	switch (select("アーチャーに転職","アーチャーの転職条件は？","なんでもありません。")) {
	case 1:
		if(Class == 3) {
			mes "[アーチャーギルド関係者]";
			mes "貴方はもうアーチャーに";
			mes "転職されています。";
			close;
		}
		if(Class != 0) {
			mes "[アーチャーギルド関係者]";
			mes "ふ…。";
			mes "見かけは初心者ではありませんが…";
			mes "他の職業を選択した以上、";
			mes "アーチャーにはなれません。";
			close;
		}
		if(Upper == 1 && OLD_CLASS != 11 && OLD_CLASS != 19 && OLD_CLASS != 20) {
			//転生前が弓手系でないとき、未調査？
			close;
		}
		break;
	case 2:
		mes "[アーチャーギルド関係者]";
		mes "それじゃ、アーチャーの転職条件に";
		mes "ついて説明します。";
		if(Class != 0) {
			if(Class == 3)
				mes "でも、貴方は既にアーチャーですが…";
			else {
				mes "でも、貴方ってもう他の職業に";
				mes "なってますが…";
			}
			mes "…まあ、いいでしょう。";
		}
		next;
		mes "[アーチャーギルド関係者]";
		mes "まず、初心者スキルレベルを";
		mes "９以上にして下さい。";
		mes "アーチャーは高度の集中力を";
		mes "必要とする職業であるため、";
		mes "マヌケな奴はアーチャーには";
		mes "絶対なれません。";
		next;
		mes "[アーチャーギルド関係者]";
		mes "アーチャー転職試験では、弓の製作に";
		mes "必要な ^ff0000木屑^000000 を集めてきてください。";
		mes "持ってきた木屑は４つのGRADEに";
		mes "分類され、その数と合わせて";
		mes "採点されます。";
		next;
		mes "[アーチャーギルド関係者]";
		mes "40点満点中^0000ff25点以上^000000であれば、";
		mes "アーチャー転職が可能です。";
		mes "木屑アイテムは、モンスター";
		mes "<ウィロー>から得られます。弱い";
		mes "モンスターですが、油断は禁物です。";
		close;
	case 3:
		close;
	}
	//case1の続き
	if(CHANGE_AC == 0) {
		mes "[アーチャーギルド関係者]";
		mes "アーチャーへの転職をお求めですか？";
		mes "そうでしたら、ここでアーチャー転職の";
		mes "申し込みをして下さい。";
		next;
		if(select("アーチャー転職申し込みをする。","やめる")==2) {
			mes "[アーチャーギルド関係者]";
			mes "あ…さよなら。";
			close;
		}
		set CHANGE_AC,1;
		mes "[アーチャーギルド関係者]";
		mes "申請書にサインをして…";
		mes "はい。これでOKです。";
		mes "これからアーチャーになるための条件を";
		mes "全て満たせば、転職が可能です。";
		mes "既に転職の条件を満たしているか、";
		mes "今確認しますが…よろしいですか？";
		next;
		if(select("はい。","いいえ。")==2) {
			mes "[アーチャーギルド関係者]";
			mes "わかりました。";
			mes "アーチャー転職条件について";
			mes "わからない部分があれば、";
			mes "私に聞いてください。";
			close;
		}
		mes "[アーチャーギルド関係者]";
		mes "確認します。";
		next;
	}
	mes "[アーチャーギルド関係者]";
	mes "お名前が… " +strcharinfo(0)+ "。ですよね？";
	next;
	if(getskilllv(1) < 9 || SkillPoint) {
		mes "[アーチャーギルド関係者]";
		mes "まだ初心者スキルレベルが足りません。";
		mes "転職条件をもう一度確認した上で、";
		mes "初心者スキルレベル９まで";
		mes "マスターして下さい。";
		close;
	}
	if(Upper == 1) {
		mes "[アーチャーギルド関係者]";
		mes "あなたは、既に転生した身";
		mes "転職試験はいりませんね";
		next;
		unequip;
		jobchange 3,1;
		skill 147,1,0;
		skill 148,1,0;
		set CHANGE_AC,0;
		mes "[アーチャーギルド関係者]";
		mes "転職おめでとうございます！";
		mes "これからもギルドの";
		mes "復興のため、頑張ってください！";
		close;
	}
	mes "[アーチャーギルド関係者]";
	mes "初心者スキルは十分ですから…";
	mes "弓製作に必要な木屑の鑑定を始めます。";
	mes "…。え〜〜";
	next;
	setarray '@amount,countitem(1066),countitem(1067),countitem(1068),countitem(1019);
	set '@total,'@amount[0]*5+'@amount[1]*3+'@amount[2]*2+'@amount[3];
	if('@total == 0) {
		mes "[アーチャーギルド関係者]";
		mes "……。";
		mes "鑑定するアイテムがありません!!";
		close;
	}
	setarray '@score,5,3,2,1;
	setarray '@grade$,"S","A","B","C";
	mes "[アーチャーギルド関係者]";
	for(set '@i,0; '@i<4; set '@i,'@i+1) {
		if('@amount['@i])
			mes "GRADE " +'@grade$['@i]+ " : " +'@amount['@i]+ " 個 / 点数 : " +('@amount['@i]*'@score['@i])+ "。";
	}
	mes "総点: ^ff0000" +'@total+ "^000000 / 40";
	next;
	if('@total < 25) {
		mes "[アーチャーギルド関係者]";
		mes "25点以下？";
		mes "25点以上にならなければ、";
		mes "合格になりません！";
		mes "もっと頑張ってください！";
		close;
	}
	delitem 1066,'@amount[0];
	delitem 1067,'@amount[1];
	delitem 1068,'@amount[2];
	delitem 1019,'@amount[3];
	mes "[アーチャーギルド関係者]";
	if('@total >= 25 && '@total < 30) {
		mes "25点以上！う〜ん";
		mes "ぎりぎりですが合格です。";
	}
	if('@total >= 30 && '@total < 40) {
		mes "30点以上！合格です！";
		mes "お疲れ様でした！";
	}
	if('@total >= 40) {
		mes "うわ！40点以上！";
		mes "凄いです！合格です！";
	}
	mes "木屑を製作係に渡します。";
	mes "これでアーチャー転職条件が";
	mes "揃いました。すぐ転職させて頂きます。";
	next;
	unequip;
	jobchange 3;
	getitem 1702,1;
	getitem 1750,rand(100);
	set CHANGE_AC,0;
	mes "[アーチャーギルド関係者]";
	mes "アーチャーへの転職、";
	mes "おめでとうございます！そして、";
	mes "これからもアーチャーギルドの";
	mes "復興のため、頑張ってください！";
	mes "あ、製作係りからプレゼントが";
	mes "届きました。お受け取りください。";
	next;
	mes "[アーチャーギルド関係者]";
	mes "アーチャーはやっぱり";
	mes "弓と矢を持たないと！";
	mes "それじゃ、頑張ってください。";
	mes "期待してます。";
	close;
}
